plugins {
    id 'java'
    id 'org.hidetake.ssh' version '2.9.0'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

sourceCompatibility = JavaVersion.VERSION_1_4
targetCompatibility = JavaVersion.VERSION_1_4

configurations {
    fat
}

jar {
    manifest {
        attributes(

                'Main-Class': 'homeassistant.display.kindle.HomeAssistantKindleDisplay',

                'Implementation-Title': 'Home Assistant Display',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Pavel Savinov (swapii@gmail.com)',

                'Extension-List': 'SDK',

                'SDK-Extension-Name': 'com.amazon.kindle.kindlet',
                'SDK-Specification-Version': '1.2',

                'Toolbar-Mode': 'persistent',
                "Font-Size-Mode": "point"

        )
    }
}

shadowJar {
    configurations = [project.configurations.fat]
    exclude '**/*.java'
}

remotes {
    kindle {
        host = kindleHost
        user = 'root'
        identity = file(kindleIdentityFile)
    }
}

def signedJar = new File(shadowJar.archivePath.parent, shadowJar.archivePath.name.replace('.jar', '-signed.jar'))

task signJar(dependsOn: 'shadowJar', description: 'Sign JAR', group: 'Build') {

    def originalJar = shadowJar.archivePath

    inputs.file(originalJar)
    outputs.file(signedJar)

    doLast {

        copy {
            from originalJar
            into signedJar.parent
            rename { name -> signedJar.name }
        }

        ['dktest', 'ditest', 'dntest'].each { key ->
            ant.signjar(
                    jar: signedJar.absolutePath,
                    keystore: new File(project.projectDir, 'developer.keystore'),
                    storepass: 'password',
                    alias: key,
            )
        }

    }

}

def resultAppFileDir = new File(project.buildDir, 'app')
def resultAppFile = new File(
        resultAppFileDir,
        signedJar.name
                .replace('-all-signed', '')
                .replace('.jar', '.azw2')
)

task azw2(dependsOn: 'signJar', description: 'Copy JAR to AZW2', group: 'Build') {
    def originalJar = signedJar
    inputs.file(originalJar)
    outputs.file(resultAppFileDir)
    doLast {
        copy {
            from originalJar
            into resultAppFileDir
            rename { name -> resultAppFile.name }
        }
    }
}

assemble.dependsOn azw2

task deployToKindleOverSSH(dependsOn: 'azw2', description: 'Deploy AZW2 to Kindle over SSH', group: 'Deploy') {
    inputs.file(resultAppFile)
    doLast {
        ssh.run {
            session(remotes.kindle) {
                put from: resultAppFile, into: '/mnt/us/documents/'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: amazonEbookLibsDir, include: ['*.jar'])
    implementation fileTree(dir: kindleJavaRuntimeLibsDir, include: ['*.jar'])
    implementation fileTree(dir: appLibsDir, include: ['*.jar'])
    fat fileTree(dir: appLibsDir, include: ['*.jar'])
}
