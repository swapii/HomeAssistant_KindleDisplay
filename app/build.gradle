plugins {
    id 'java'
}

sourceCompatibility = JavaVersion.VERSION_1_4
targetCompatibility = JavaVersion.VERSION_1_4

jar {
    manifest {
        attributes(

                'Main-Class': 'smarthouse.kindledisplay.SmartHouseDisplay',

                'Implementation-Title': 'Smart House Display',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Pavel Savinov (swapii@gmail.com)',

                'Extension-List': 'SDK',

                'SDK-Extension-Name': 'com.amazon.kindle.kindlet',
                'SDK-Specification-Version': '1.2',

                'Toolbar-Mode': 'persistent',
                "Font-Size-Mode": "point"

        )
    }
}

task signJar(dependsOn: 'jar', description: 'Sign JAR', group: 'Build') {
    doLast {
        ['dktest', 'ditest', 'dntest'].each { key ->
            ant.signjar(
                    jar: jar.archivePath,
                    keystore: new File(project.projectDir, 'developer.keystore'),
                    storepass: 'password',
                    alias: key,
            )
        }
    }
}

task azw2(dependsOn: 'signJar', description: 'Copy JAR to AZW2', group: 'Build') {
    doLast {
        copy {
            from jar.archivePath
            into new File(project.buildDir, 'app')
            rename { name -> name.replace('.jar', '.azw2') }
        }
    }
}

assemble.finalizedBy azw2

dependencies {
    implementation fileTree(dir: amazonEbookLibsDir, include: ['*.jar'])
}
